# Name: ccCA_CBS-2-Atom
#
# *************************************** DESCRIPTION ***********************************************
#
#   This is the ccCA-CBS-2 scheme (the version for atoms) by N. J. DeYonker, T. R. Cundari, and 
#   A. K. Wilson based on: J. Chem. Phys. 124, 114104 (2006) 
#
# *************************************** LITERATURE  ************************************************
#
# Description of the protocol                           : J. Chem. Phys. 124, 114104 (2006)
#
# ***************************************   DETAILS   ************************************************
# The protocol consists of 7 steps
#
# ******************************************   NOTES   ************************************************
# NOTE 1:  For reference energy we use the extapolated CBS/MP2 energy calculated with the extrapolation 
#          scheme proposed by S.S. Xantheas and T.H. Dunning (JCP 97, 18-19 (1993) and David Feller  
#          (J. Chem. Phys. 1993, 98, 7059-7071) that uses the function:
#                            E(X) = E(CBS)+ Bexp(-Cx)
#          for the total energy. Using this form for the energy and DZ,TZ and QZ basis sets the E(CBS) 
#          can be estimated as:
#                E(CBS)= (E(DZ)*E(QZ)-E(TZ)*E(TZ))/(E(QZ)+E(DZ)-2E(TZ)
# NOTE 2:  For MP2 we use the RI version
# NOTE 3:  Even though we do not need the optimization and frequency steps we keep the (adjusted) just 
#          for consistency with the normal version
#
# ******************************************  METHOD   ************************************************
#
#

# Define some variables
variable ZPE;                          # The ZPE correction 
variable Scale, ZPEScaled;             # The scaling for the ZPE
variable EMP2_aDZ, EMP2_aTZ, EMP2_aQZ; # MP2 with small and big basis sets
variable D;                            # The D variable
variable ERef;                         # The total Reference energy
variable EMP2_TZ, EQCISDT_TZ, DE_QCI;  # The total energies with QCISD(T) and MP2 for higher order correction
variable EMP2Full,  DECV;              # Full MP2, MP2 with frozen core and DCV
variable HartreeToKcal;                
variable FinalEnergy;                  

#----------------------------------------------------------
# (Calculation 1)
#Optimization and ZPE 
New_Step
! B3LYP 6-31G* VeryTightSCF Opt Freq DefGrid3
STEP_END
Scale = 0.9854;                
read ZPE = THERMO_ZPE[1];      
ZPEScaled = Scale*ZPE;         

#---------------------------------------------------------
# (Calculation 2)
# RI-MP2 Energy with aug-cc-pVDZ (for CBS reference energy)
New_Step
 !RI-MP2 aug-cc-pVDZ aug-cc-pVDZ/C VeryTightSCF
STEP_END
read EMP2_aDZ = MP2_TOTAL_ENERGY[2];  

#---------------------------------------------------------
# (Calculation 3)
# RI-MP2 Energy with aug-cc-pVTZ (for CBS reference energy)
New_Step
 !RI-MP2 aug-cc-pVTZ aug-cc-pVTZ/C VeryTightSCF
STEP_END
read EMP2_aTZ = MP2_TOTAL_ENERGY[3];   

#---------------------------------------------------------
# (Calculation 4)
# RI-MP2 Energy with aug-cc-pVQZ (for CBS reference energy)
New_Step
 !RI-MP2 aug-cc-pVQZ aug-cc-pVQZ/C VeryTightSCF
STEP_END
read EMP2_aQZ = MP2_TOTAL_ENERGY[4];     
D = (exp(-1)-exp(-3))/(exp(-9)-exp(-5)); 
ERef = (e*EMP2_aTZ-EMP2_aDZ + D*(-exp(-1)*EMP2_aTZ+EMP2_aQZ))/((e-1)-D*(exp(-1)-1)); 

#---------------------------------------------------------
# (Calculation 5)
# RI-MP2 Energy with cc-pVTZ (for QCISD(T) correction) 
New_Step
 !MP2 cc-pVTZ cc-pVTZ/C VeryTightSCF
STEP_END
read EMP2_TZ = MP2_TOTAL_ENERGY[5];     

#---------------------------------------------------------
# (Calculation 6)
# QCISD(T) Energy with cc-pVTZ (for QCISD(T) correction
New_Step
  !QCISD(T) cc-pVTZ VeryTightSCF
STEP_END
read EQCISDT_TZ = MDCI_TOTAL_ENERGY[6];   
DE_QCI = EQCISDT_TZ - EMP2_TZ;       

#---------------------------------------------------------
# (Calculation 7)
# MP2 Full
New_Step
! MP2 NoFrozenCore aug-cc-pCVTZ VeryTightSCF
STEP_END
read EMP2Full = MP2_TOTAL_ENERGY[7];  
DECV = EMP2Full - EMP2_aTZ;           

#---------------------------------------------------------
FinalEnergy = ERef + ZPEScaled + DE_QCI + DECV; 
END
